import java.text.SimpleDateFormat

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: "androidx.navigation.safeargs.kotlin"

android {
    compileSdkVersion 29
    defaultConfig {
        applicationId "com.vitaliimalone.simpletodo"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            minifyEnabled false
            versionNameSuffix "-SNAPSHOT"
            resValue("string", "app_name", "Simple To-Do(Debug)")
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            resValue("string", "app_name", "Simple To-Do")
        }
    }
    android.applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "${variant.name}-${variant.versionName}-${buildTime()}.apk"
        }
    }
}

androidExtensions {
    experimental = true
}

static def buildTime() {
    def df = new SimpleDateFormat("yyyy-MM-dd")
    df.setTimeZone(TimeZone.getTimeZone("UTC"))
    return df.format(new Date())
}

dependencies {

    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // core
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "androidx.core:core-ktx:1.1.0"
    implementation "androidx.appcompat:appcompat:1.1.0"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0-beta01"
    implementation 'androidx.lifecycle:lifecycle-extensions:2.1.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'

    // ui
    implementation "androidx.viewpager2:viewpager2:1.0.0-beta05"
    implementation "androidx.constraintlayout:constraintlayout:2.0.0-beta2"
    implementation "androidx.recyclerview:recyclerview:1.0.0"
    implementation "com.google.android.material:material:1.1.0-beta01"

    // async
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.0'

    // navigation
    implementation "androidx.navigation:navigation-fragment-ktx:2.1.0"
    implementation "androidx.navigation:navigation-ui-ktx:2.1.0"

    // db
    implementation "androidx.room:room-runtime:2.2.0"
    implementation "androidx.room:room-ktx:2.2.0"
    kapt "androidx.room:room-compiler:2.2.0"
    implementation 'com.google.code.gson:gson:2.8.6'

    // di
    implementation "org.koin:koin-androidx-viewmodel:2.0.1"

    // time
    implementation 'com.jakewharton.threetenabp:threetenabp:1.2.1'
}
